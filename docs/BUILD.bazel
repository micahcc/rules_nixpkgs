load("stardoc.bzl", "stardoc")

stardoc(
    name = "nixpkgs",
    out = "nixpkgs.md",
    input = "//nixpkgs:nixpkgs.bzl",
    deps = ["//nixpkgs"],
    symbol_names = [
        "nixpkgs_git_repository",
        "nixpkgs_local_repository",
        "nixpkgs_package",
        "nixpkgs_cc_configure",
        "nixpkgs_python_configure",
        "nixpkgs_sh_posix_configure",
    ],
)

stardoc(
    name = "go",
    out = "toolchains/go.md",
    input = "//nixpkgs:toolchains/go.bzl",
    deps = ["//nixpkgs:toolchains_go"],
    symbol_names = [
        "nixpkgs_go_configure",
    ],
)

genrule(
    name = "readme",
    srcs = [
        "README.md.tpl",
        "nixpkgs.md",
        "toolchains/go.md",
    ],
    outs = ["README.md"],
    toolchains = ["@rules_sh//sh/posix:make_variables"],
    cmd = """\
$(POSIX_AWK) \\
  <$(execpath README.md.tpl) \\
  >$(OUTS) \\
  '{
      if (/%nixpkgs%/) {
          RS="\\0";
          getline content <"$(execpath nixpkgs.md)";
          print content
      } else if (/%toolchains_go%/) {
          RS="\\0";
          getline content <"$(execpath toolchains/go.md)";
          print content
      } else {
          print
      }
  }'
""",
)

sh_test(
    name = "check-readme",
    srcs = ["check-readme.sh"],
    deps = ["@bazel_tools//tools/bash/runfiles"],
    data = ["//:README.md", "README.md"],
)

sh_binary(
    name = "update-readme",
    srcs = ["update-readme.sh"],
    deps = ["@bazel_tools//tools/bash/runfiles"],
    data = ["README.md"],
)
